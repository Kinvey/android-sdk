apply plugin: 'java'
description = 'JavaLib'

sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
}

configurations {
    jaxDoclet
}


dependencies {
    jaxDoclet(group: 'com.kinvey', name: 'doclava-kinvey-custom', version: '1.0.6a')
    compile('com.google.http-client:google-http-client:1.19.0') {
        exclude(module: 'xpp3')
        exclude(module: 'httpclient')
        exclude(module: 'junit')
        exclude(module: 'android')
        exclude(module: 'guava-jdk5')
    }
    compile('com.google.api-client:google-api-client:1.19.0') {
        exclude(module: 'xpp3')
        exclude(module: 'android')
    }
    compile('com.google.api-client:google-api-client-android:1.19.0') {
        exclude(module: 'google-play-services')
        exclude(module: 'xpp3')
    }
    compile 'com.google.http-client:google-http-client-android:1.19.0'
    compile('com.google.http-client:google-http-client-gson:1.19.0') {
        exclude(module: 'xpp3')
    }
    compile 'com.google.code.gson:gson:2.1'
    compile('com.google.http-client:google-http-client-jackson:1.19.0') {
        exclude(module: 'xpp3')
    }
    compile 'org.codehaus.jackson:jackson-core-asl:1.9.12'
    compile project(':java-api-core')
    testCompile 'junit:junit:4.10'
    testCompile 'org.robolectric:robolectric:2.1.1'
    compile('com.google.android:android:4.0.1.2') {
        /* This dependency was originally in the Maven provided scope, but the project was not of type war.
        This behavior is not yet supported by Gradle, so this dependency has been converted to a compile dependency.
        Please review and delete this closure when resolved. */
    }
}

task docgen(type: Javadoc) {
    

    source = sourceSets.main.java.srcDirs

    options.docletpath = files("../tools/doclava-kinvey-custom.jar").asType(List)
    options.doclet = "com.google.doclava.Doclava"
    maxMemory = "1024m"
    classpath = configurations.compile
    title = null

    options.bootClasspath new File(System.getenv('JAVA_HOME') + "/jre/lib/rt.jar")
    options.overview = "javadoc/overview.html"

    options.addStringOption("quiet")
    options.addStringOption("hdf", "project.name JavaLib")
    options.addStringOption("stubs", "target/stubs")
    options.addStringOption("federate com.kinvey.java", "http://devcenter.kinvey.com/android/reference/api/java/reference/")
    options.addStringOption("federationxml com.kinvey.java", "../java-api-core/target/apidocs/api.xml")
    options.addStringOption("d", "../../../devcenter/content/reference/java/api")
}

cleanTest.mustRunAfter clean
cleanDocgen.mustRunAfter cleanTest
build.mustRunAfter cleanDocgen
docgen.mustRunAfter build
