apply plugin: 'com.android.library'
apply plugin: 'findbugs'
description = 'AndroidLib'



android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            manifest.srcFile 'AndroidManifest.xml'

        }
        test{
            java{
                srcDir 'test/com'
            }
        }

        androidTest.setRoot('test')
    }

    lintOptions {
          abortOnError false
    }
}

configurations {
    jaxDoclet
    classpaths
    compile.exclude module: 'guava-jdk5'
}


buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3+'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.+'
    }
}

dependencies {
    jaxDoclet(group: 'com.kinvey', name: 'doclava-kinvey-custom', version: '1.0.6a')
    compile project(':java-api-core')
    /*compile('com.google.http-client:google-http-client:1.19.0') {
            exclude(module: 'xpp3')
            exclude(module: 'httpclient')
            exclude(module: 'junit')
            exclude(module: 'android')
            exclude(module: 'guava-jdk5')
        }*/
    compile('com.google.api-client:google-api-client:1.19.0') {
        exclude(module: 'xpp3')
        exclude(module: 'android')
    }
    compile('com.google.api-client:google-api-client-android:1.19.0') {
        exclude(module: 'google-play-services')
        exclude(module: 'xpp3')
    }
    compile('com.google.http-client:google-http-client-android:1.19.0') {
        exclude(module: 'xpp3')
        exclude(module: 'httpclient')
        exclude(module: 'junit')
        exclude(module: 'android')
        exclude(module: 'guava-jdk5')
    }
    compile('com.google.http-client:google-http-client-gson:1.19.0') {
        exclude(module: 'xpp3')
    }
    compile 'com.google.code.gson:gson:2.1'
    compile('com.google.http-client:google-http-client-jackson:1.19.0') {
        exclude(module: 'xpp3')
    }
    //compile 'com.google.guava:guava:18.0'
    compile 'org.codehaus.jackson:jackson-core-asl:1.9.12'
    testCompile 'junit:junit:4.10'
    testCompile('org.robolectric:robolectric:2.2') {
        exclude(module: 'support-v4')
    }
    classpaths files(new File(System.getenv('ANDROID_HOME') + '/platforms/android-19/android.jar'))
    classpaths files('build/classes/release')
    compile 'com.google.android.gms:play-services:8.4.0'
}

task docgen(type: Javadoc) {
    

    source = android.sourceSets.main.java.srcDirs

    options.docletpath = files("../tools/doclava-kinvey-custom.jar").asType(List)
    options.doclet = "com.google.doclava.Doclava"
    maxMemory = "1024m"
    classpath = configurations.classpaths + configurations.compile + configurations.testCompile
    title = null

    options.bootClasspath new File(System.getenv('JAVA_HOME') + "/jre/lib/rt.jar")
    options.overview = "javadoc/overview.html"

    options.addStringOption("quiet")
    options.addStringOption("hdf", "project.name AndroidLib")
    options.addStringOption("stubs", "target/stubs")
    options.addStringOption("federate com.kinvey.java", "http://devcenter.kinvey.com/android/reference/api/java/reference/")
    options.addStringOption("federationxml com.kinvey.java", "../java-api-core/target/apidocs/api.xml")
    //options.addStringOption("d", "target/apidocs")
    options.addStringOption("d", "../../../devcenter/content/reference/android/api")
}

task superJar(type: Jar){
  from configurations.compile
  archiveName = "superjar.jar"
  destinationDir = new File("build/outputs/superjar")
}

findbugs {
    sourceSets = []
}

task findbugs(type: FindBugs, dependsOn: assembleDebug) {
    ignoreFailures = true
    classes = files("$project.buildDir/intermediates/classes/debug")
    ignoreFailures = true
    effort = "max"
    reportLevel = "medium"
    excludeFilter = file("$rootProject.rootDir/tools/findbugs/exclude.xml")

    source = fileTree('src')
    classpath = files()
    reports {
        xml.enabled = true
        xml.withMessages = true
        html.enabled = !xml.isEnabled()
        xml.destination = "$rootProject.rootDir/reports/findbugsReports/${project.name}/main.xml"
        html.destination = "$rootProject.rootDir/reports/findbugsReports/${project.name}/main.html"
    }
}



//cleanTest.mustRunAfter clean
cleanDocgen.mustRunAfter clean
build.mustRunAfter cleanDocgen
docgen.mustRunAfter build


